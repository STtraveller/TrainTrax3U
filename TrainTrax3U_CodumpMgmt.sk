options:
    # # # # # # # # # # # # # # # # # # # # # # # # # # #
    # TrainTrax3U Train Tracker Assistant               #
    # // CodumpMgmt - Component and Dump Manager        #
    # // Requires TrainTrax3U_Core                      #
    #                                                   #
    # Author: MinecraftSBC, blackon108 (Algorithm)      #
    # # # # # # # # # # # # # # # # # # # # # # # # # # #
    prefix: &3[TRTRX-CM] &r
    build: A17Y27R07
    license: Copyright Â© 2017 SBCRET <SBCRET@gmx.com> under DBAD v1.1 as published by Philip Sturgeon.
    
    # Using {TrainTrax3.Param.PointZero::*}
    # Using {TrainTrax3.Dump.PointZero::*}
    # Using {TrainTrax3.Dump.LastUpdate}

function TRTRX_GetDump(x: string) :: string:
    if {TrainTrax3.Dump.LastUpdate} is not set:
        set {_y} to true
    else if difference between {TrainTrax3.Dump.LastUpdate} and now > 15 seconds:
        set {_y} to true
    else if {TrainTrax3.Dump.PointZero::%{_x}%} is not set:
        set {_y} to true
    if {_y} is true:
        loop {TrainTrax3.Param.PointZero::*}:
            set {TrainTrax3.Dump.PointZero::%loop-index%} to "%TRTRX_GetRDump(loop-index)%"
        set {TrainTrax3.Dump.LastUpdate} to now
    return {TrainTrax3.Dump.PointZero::%{_x}%}
    
function TRTRX_GetRDump(x: string) :: string:
    set {_args::*} to {TrainTrax3.Param.PointZero::%{_x}%} split at " "
    if size of {_args::*} = 5:
        return "%FindTrain({_args::1} parsed as integer, {_args::2} parsed as integer, {_args::3} parsed as integer, {_args::4}, {_args::5} parsed as integer)%"
    else:
        return ""

function TRTRX_GetParam(x: string) :: string:
    return {TrainTrax3.Param.PointZero::%{_x}%}
  
command /trx-cm <string> [<string>] [<string>] [<string>]:
    aliases: /trxcm, /trtrx-cm, /trtrxcm
    usage: /trx-cm < p0 > < P0 ident / - > < set / remove / list / find / dump / rdump > < ... >
    trigger:
        if arg-1 is "p0":
            if arg-3 is "set":
                # Command Validation
                if size of arg-4 split at " " = 5:
                    set {TrainTrax3.Param.PointZero::%arg-2%} to arg-4
                    message "{@prefix} Point Zero %arg-2% set as %arg-4%"
                    message "{@prefix} Showing current dump."
                    make player execute "/trx-cm p0 %arg-2% rdump"
                else:
                    message "{@prefix} Point Zero Parameter length mismatch."
                    message "{@prefix} Make sure you have specify all 5 parameters."
                    message "{@prefix} <x> <y> <z> <n/e/w/s> <itin>"
            else if arg-3 is "remove":
                delete {TrainTrax3.Param.PointZero::%arg-2%}
                message "{@prefix} Point Zero %arg-2% removed."
            else if arg-3 is "list":
                message "{@prefix} Listing all Point Zeros..."
                message "= = = ID: PARAM = = ="
                loop {TrainTrax3.Param.PointZero::*}:
                    message "%loop-index% : %loop-value%"
                message "= = = EOF : %size of {TrainTrax3.Param.PointZero::*}% ENTRIES = = ="                
            else if arg-3 is "find":
                message "{@prefix} Searching for Point Zero %arg-2%..."
                message "= = = ID: PARAM = = ="
                loop {TrainTrax3.Param.PointZero::*}:
                    if loop-index contains arg-2:
                        message "%loop-index% : %loop-value%"
                message "= = = EOF : %size of {TrainTrax3.Param.PointZero::*}% ENTRIES = = ="
            else if arg-3 is "dump":
                message "{@prefix} = POINT ZERO %arg-2%"
                message "{@prefix} LAST UPDATE > %unix timestamp of {TrainTrax3.Dump.LastUpdate}%"
                message "{@prefix} BUFFERED DUMP > %{TrainTrax3.Dump.PointZero::%arg-2%}%"
            else if arg-3 is "rdump":
                set {_args::*} to {TrainTrax3.Param.PointZero::%arg-2%} split at " "
                message "{@prefix} = POINT ZERO %arg-2%"
                message "{@prefix} PARAM > %{TrainTrax3.Param.PointZero::%arg-2%}%"
                message "{@prefix} REAL TIME DUMP > %FindTrain({_args::1} parsed as integer, {_args::2} parsed as integer, {_args::3} parsed as integer, {_args::4}, {_args::5} parsed as integer)%"